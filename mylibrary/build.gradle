plugins {
    id 'com.android.library'
}

apply plugin: 'maven-publish'  // 1. Apply plugin
// 2. Declare library details
ext {
    mGroupId = "com.huyle"
    mArtifactId = "mylibrary"
    mVersionCode = 1
    mVersionName = "0.0.3"

    mLibraryName = "mylibrary"
    mLibraryDescription = "mylibrary for simple things!"
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

// 3. Declare task for creation of android sources.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
// 4. Make configuration for publishing artifact.
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                // artifact androidSourcesJar
                artifact("$buildDir/outputs/aar/${mArtifactId}-debug.aar")

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        def localProperties = new Properties()
        localProperties.load(new FileInputStream(rootProject.file("github.properties")))


        // 5. Update repository details and credentials.
        repositories {//MichaelHuy/testmaven
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/MichaelHuy/testmaven")
                credentials {
                    username = localProperties.getProperty("gpr.usr")
                    password = localProperties.getProperty("gpr.key")
                }
            }
        }
    }
}

// Assembling should be performed before publishing package
publish.dependsOn assemble